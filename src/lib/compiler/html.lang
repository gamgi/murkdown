RULES FOR html PRODUCE text/html

PREPROCESS RULES:
/* list section */
[LIST...] [SEC...]$
  NOOP

/* code section */
[...CODE...] [SEC...]$
  NOOP

[SEC...]$
  IS PARAGRAPHABLE

[...IMAGE...]$
  IS REF-BY-COPY

COMPILE RULES:
/* root */
^[...]$
  PUSH join "\n"

/* root code section */
^[...] [SEC...] [...CODE...] [SEC...]$
  WRITE indent
  WRITE "<pre><code data-noescape data-language-$language data-line-numbers>"
  YIELD
  WRITE "\n"
  WRITE indent
  WRITE "</code></pre>"


[...TIP...]$
  IS COMPOSABLE
  PUSH class "tip"

/* code section */
[...CODE...] [SEC...]$
  WRITE indent
  WRITE "<pre><code data-noescape data-language-$language data-line-numbers>"
  YIELD
  WRITE "\n"
  WRITE "</code></pre>"

/* section */
[SEC...]$
  NOOP

/* figure block */
[...FIGURE...]$
  PUSH indent "  "
  WRITE "<figure>\n"
  YIELD
  WRITE "\n"
  POP indent
  WRITE indent
  WRITE "</figure>"

/* figure image block */
[...FIGURE...] [SEC...] [...IMAGE...]$
  WRITE indent
  WRITE "<img src=\"$ref\" />\n"
  WRITE indent
  WRITE "\n"
  WRITE indent
  WRITE "<figcaption>\n"
  PUSH indent "  "
  YIELD
  WRITE "\n"
  POP indent
  WRITE indent
  WRITE "</figcaption>"

/* image block */
[...IMAGE...]$
  WRITE indent
  WRITE "<img src=\"$ref\" alt=\""
  YIELD
  WRITE "\" />"

/* code block */
[...CODE...]$
  SET language "$language"
  YIELD
  SET language ""

/* heading block */
[...]( [SEC...] [...HEADING...]){3}$
  SET prefix "<h2>"
  SET suffix "</h2>"
  YIELD
  POP prefix
  POP suffix

[...]( [SEC...] [...HEADING...]){2}$
  SET prefix "<h2>"
  SET suffix "</h2>"
  YIELD
  POP prefix
  POP suffix

[...]( [SEC...] [...HEADING...]){1}$
  PUSH prefix "<h1>"
  PUSH suffix "</h1>"
  YIELD
  POP prefix
  POP suffix

/* list */
[LIST...]$
  PUSH indent "  "
  WRITE "<ul>\n"
  YIELD
  WRITE "\n"
  POP indent
  WRITE indent
  WRITE "</ul>"

[LIST...] [SEC...]$
  YIELD

[LIST...] [SEC...] LINE$
  PUSH prefix "<li>"
  PUSH suffix "</li>"
  WRITEALL indent
  WRITEALL prefix
  WRITE "\v"
  WRITEALL suffix
  POP prefix
  POP suffix

/* paragraph */
[PAR...]$
  WRITE indent
  WRITE "<p>\n"
  PUSH indent "  "
  YIELD
  POP indent
  WRITE "\n"
  WRITE indent
  WRITE "</p>"

/* block */
[...]$
  PUSH indent "  "
  WRITE "<div class=\""
  WRITEALL class
  WRITE "\">\n"
  YIELD
  WRITE "\n"
  POP indent
  WRITE indent
  WRITE "</div>"

/* code line */
[...CODE...] [SEC...] LINE$
  WRITEALL prefix
  WRITE "\v"
  WRITEALL suffix

/* line */
LINE$
  WRITEALL indent
  WRITEALL prefix
  WRITE "\v"
  WRITEALL suffix
