RULES FOR html PRODUCE text/html

/* ------------------------------------------------ */
PREPROCESS RULES:
/* list */
[LIST...] [SEC...]$
  NOOP

/* image */
[...IMAGE...]$
  IS REF-BY-COPY

/* code */
[...CODE...] [SEC...]$
  NOOP

/* other */
[SEC...]$
  IS PARAGRAPHABLE

/* ------------------------------------------------ */
COMPILE RULES:
/* root */
^[...]$
  SET join "\n"

[...TIP...]$
  IS COMPOSABLE
  PUSH class "tip"

[...QUOTE...]$
  IS COMPOSABLE
  PUSH class "quote"

/* tabs */
[...TABS...] [SEC...] [...TABS...]$
  WRITEALL indent
  WRITE "<input type=\"radio\" name=\"$tabid-r\" id=\"$tabid-\i\" value=\"dummy\" $checked />\n"
  DRAIN checked
  WRITEALL indent
  WRITE "<label for=\"$tabid-\i\">$id</label>\n"
  /* start a new tab */
  SET checked "checked"
  SET tabid "\r"
  WRITE "<div class=\"tabs tabs-content $class:j\" name=\"$tabid-f\">\n"
  DRAIN class
  PUSH indent "  "
  YIELD
  WRITE "\n"
  POP indent
  WRITEALL indent
  WRITE "</div>\n"

[...TABS...]$
  WRITEALL indent
  SET checked "checked"
  SET tabid "\r"
  WRITE "<div class=\"tabs $class:j\" name=\"$tabid-f\">\n"
  DRAIN class
  PUSH indent "  "
  YIELD
  WRITE "\n"
  POP indent
  WRITEALL indent
  WRITE "</div>"

[...TABS...] [SEC...] [...]$
  IS COMPOSABLE
  WRITEALL indent
  WRITE "<input type=\"radio\" name=\"$tabid-r\" id=\"$tabid-\i\" value=\"dummy\" $checked />\n"
  DRAIN checked
  WRITEALL indent
  WRITE "<label for=\"$tabid-\i\">$id</label>\n"
  PUSH class "tabs-content"

/* image & figure */
[...FIGURE...]$
  PUSH indent "  "
  WRITE "<figure>\n"
  YIELD
  WRITE "\n"
  POP indent
  WRITEALL indent
  WRITE "</figure>"

[...FIGURE...] [SEC...] [...IMAGE...]$
  WRITEALL indent
  WRITE "<img src=\"$ref\" />\n"
  WRITEALL indent
  WRITE "\n"
  WRITEALL indent
  WRITE "<figcaption>\n"
  PUSH indent "  "
  YIELD
  WRITE "\n"
  POP indent
  WRITEALL indent
  WRITE "</figcaption>"

[...IMAGE...]$
  WRITEALL indent
  WRITE "<img src=\"$ref\" class=\"$class:j\" alt=\""
  YIELD
  WRITE "\" />"

[...IMAGE...] LINE$
  WRITE "\v"

[...IMAGE...] [...]$
  NOOP

/* code */
[CODE]$
  SET language "$language"
  YIELD
  SET language ""

[...CODE...]$
  SET language "$language"
  YIELD
  SET language ""

[...CODE...] [SEC...]$
  WRITEALL indent
  WRITE "<pre class=\"code $class:j\"><code class=\"language-$language\">"
  DRAIN class
  YIELD
  WRITE "\n"
  WRITE "</code></pre>"

[...CODE...] [SEC...] LINE$
  WRITEALL prefix
  WRITE "\v"
  WRITEALL suffix

/* heading */
[...]( [SEC...] [...HEADING...]){3}$
  SET prefix "<h2>"
  SET suffix "</h2>"
  YIELD
  POP prefix
  POP suffix

[...]( [SEC...] [...HEADING...]){2}$
  SET prefix "<h2>"
  SET suffix "</h2>"
  YIELD
  POP prefix
  POP suffix

[...]( [SEC...] [...HEADING...]){1}$
  PUSH prefix "<h1>"
  PUSH suffix "</h1>"
  YIELD
  POP prefix
  POP suffix

/* list */
[...LIST...]$
  WRITEALL indent
  WRITE "<ul class=\"$class:j\">\n"
  DRAIN class
  PUSH indent "  "
  YIELD
  WRITE "\n"
  POP indent
  WRITEALL indent
  WRITE "</ul>"

[...LIST...] [SEC...]$
  IS COMPOSABLE
  YIELD

[...LIST...] [SEC...] [PAR...] ASD$
  NOOP

[...LIST...] [SEC...] [...]$
  IS COMPOSABLE
  WRITEALL indent
  WRITE "<li>\n"
  PUSH indent "  "
  YIELD
  POP indent
  WRITE "\n"
  WRITEALL indent
  WRITE "</li>"

[...LIST...] [SEC...] LINE$
  IS COMPOSABLE
  PUSH prefix "<li>"
  PUSH suffix "</li>"
  YIELD
  POP prefix
  POP suffix

/* link */
[...LINK...]$
  IS COMPOSABLE
  WRITEALL indent
  WRITE "<a href=\"$href\">\n"
  PUSH indent "  "
  YIELD
  POP indent
  WRITE "\n"
  WRITEALL indent
  WRITE "</a>"

/* paragraph */
[PAR...]$
  WRITEALL indent
  WRITE "<p>\n"
  PUSH indent "  "
  YIELD
  POP indent
  WRITE "\n"
  WRITEALL indent
  WRITE "</p>"

/* other */
[SEC...]$
  NOOP

[...]$
  WRITEALL indent
  WRITE "<div class=\"$class:j\">\n"
  DRAIN class
  PUSH indent "  "
  YIELD
  WRITE "\n"
  POP indent
  WRITEALL indent
  WRITE "</div>"

LINE$
  WRITEALL indent
  WRITEALL prefix
  WRITE "\v"
  WRITEALL suffix
